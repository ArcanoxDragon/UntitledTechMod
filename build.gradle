buildscript {
    repositories {
        maven { url = "https://files.minecraftforge.net/maven" }
        maven { url = "https://dl.bintray.com/kotlin/kotlin-eap" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.10"
    }
}

plugins {
    id "com.wynprice.cursemaven" version "2.1.1"
}

apply plugin: 'kotlin'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'

repositories {
    maven {
        name = "Kotlin Early Access"
        url = "https://dl.bintray.com/kotlin/kotlin-eap"
    }
}

version = "${mod_version}"
group = "${mod_group}"    // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "${mod_id}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

tasks.build.dependsOn kotlinSourcesJar

minecraft {
    // MCP Mappings
    mappings channel: 'snapshot', version: "${mcp_mappings}"

    runs {
        client {
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            properties 'forge.logging.console.level': 'debug'
            workingDirectory project.file('run').canonicalPath
            source sourceSets.main
        }
        server {
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            properties 'forge.logging.console.level': 'debug'
            workingDirectory project.file('run').canonicalPath
            source sourceSets.main
        }
        data {
            workingDirectory project.file('run').canonicalPath
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'techmod', '--all', '--output', file('src/generated/resources/'), '--existing', sourceSets.main.resources.srcDirs[0]
            source sourceSets.main
        }
    }
}

dependencies {
    // Forge itself
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    // Kotlin stuff
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'

    // Other mods
    if (findProject(':ArcanoxLib') != null) {
        compile project(':ArcanoxLib')
    } else {
        compile fg.deobf(project.dependencies.create("me.arcanox.lib:arcanoxlib:${arcanoxlib_version}") {
            transitive = false
        })
    }
    if (findProject(':Multipart') != null) {
        compile project(':Multipart')
    } else {
        compile fg.deobf(project.dependencies.create("me.arcanox.multipart:mcmultipart:${multipart_version}") {
            transitive = false
        })
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
        languageVersion = '1.4'
        apiVersion = '1.4'
        freeCompilerArgs = ["-Xopt-in=kotlin.ExperimentalStdlibApi"]
    }
}

kotlinSourcesJar {
    from(sourceSets.main.kotlin.srcDirs)
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        expand 'version': project.version,
                'modid': mod_id,
                'modname': mod_name
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
                "Specification-Title"     : "${mod_id}",
                "Specification-Vendor"    : "${mod_author}",
                "Specification-Version"   : "${mod_version}",
                "Implementation-Title"    : "${project.name}",
                "Implementation-Vendor"   : "${mod_author}",
                "Implementation-Version"  : "${mod_version}",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

task apiJar(type: Jar) {
    archiveClassifier.set("api")

    from(sourceSets.main.output) {
        include "me/arcanox/techmod/api/**"
        exclude "me/arcanox/techmod/api/impl/**"
        exclude "me/arcanox/techmod/api/ApiInit.class"
    }
}

artifacts {
    archives apiJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact apiJar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}